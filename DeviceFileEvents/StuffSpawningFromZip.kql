Adapted from @rpargman & @secops_addy on Twitter

DeviceFileEvents
| where TimeGenerated > ago(90d)
| where FileOriginReferrerUrl has ".zip"
| extend FileType = extract(@"([^\.]+$)", 1, FileName) // No inbuilt filetype column so we grab everything after the last . to find out
| summarize 
FileList = make_set(FileName), // Grab the names of all files spawned and stick in a column
FileTypeList = make_set(FileType) // Grab the types of files we've got from the regex
        by DeviceName, RequestAccountName, FileOriginReferrerUrl
| extend FileCount = array_length(FileList) // Both these count how many unique values are in each column
| extend FileTypeCount= array_length(FileTypeList)
| where FileList has_any (".iso", ".img", ".cmd", ".bat", ".dll", ".db", ".dat") // These are common file types I've seen spawning from .zip files
| project DeviceName, RequestAccountName, FileOriginReferrerUrl, FileList, FileTypeList, FileCount, FileTypeCount
